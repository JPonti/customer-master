config {
  type: "incremental",
  tags:["shopee"],
  uniqueKey: ["customer_guid","product","address_id"],
  bigquery: {
    partitionBy: "date",
    updatePartitionFilter:
        "date >= DATE_SUB(CURRENT_DATE('Asia/Manila'), interval 1 DAY)"
  }
}

-- select *
-- from ${ref("source_table")}
-- ${when(incremental(), `where timestamp > (select max(timestamp) from ${self()})`)}
SELECT 
  DISTINCT
  DATE(a1.created_at) as date,
  a.account_id,
  a.customer_guid,
  t.product,
  a1.id as address_id,
  a1.name,
  a1.email,
  a1.phone_number,
  a1.mobile_number,
  a1.line_1,
  a1.district,
  a1.city,
  a1.state as province,
  a1.region
FROM ${ref("dim_shopee_transactions")} t
JOIN ${ref("dim_customer_accounts")} a ON a.customer_guid = t.customer_guid  AND a.product IN ('SHP (Reverse Logistics)')
JOIN ${ref("consumer_orders")}  o ON t.transaction_id = o.id  
JOIN ${ref("core_addresses")} a1 ON o.delivery_address_id = a1.id
WHERE a1.type = 'delivery' AND o.party_id  = 661556  AND a1.name <> 'Shopee Warehouse'
  ${when(incremental(), `AND DATE(transaction_date) >= DATE_SUB(CURRENT_DATE('Asia/Manila'), INTERVAL 1 DAY)
  AND DATE(o.base_loaded_at) >= DATE_SUB(CURRENT_DATE('Asia/Manila'), INTERVAL 1 DAY)
  AND DATE(a1.base_loaded_at) >= DATE_SUB(CURRENT_DATE('Asia/Manila'), INTERVAL 1 DAY)`)}

UNION DISTINCT 

SELECT 
  DISTINCT
  DATE(a1.created_at) as date,
  a.account_id,
  a.customer_guid,
  t.product,
  a1.id as address_id,
  a1.name,
  a1.email,
  a1.phone_number,
  a1.mobile_number,
  a1.line_1,
  a1.district,
  a1.city,
  a1.state as province,
  a1.region
FROM ${ref("dim_shopee_transactions")} t
JOIN ${ref("dim_customer_accounts")} a ON a.customer_guid = t.customer_guid  AND a.product IN ('Shopee (Tiered Pricing)', 'Shopee')
JOIN ${ref("consumer_orders")}  o ON t.transaction_id = o.id  
JOIN ${ref("core_addresses")} a1 ON o.pickup_address_id = a1.id
WHERE a1.type = 'pickup' AND o.party_id  IN (6, 317799)  --AND a1.name <> 'Shopee Warehouse'
  ${when(incremental(), `AND DATE(transaction_date) >= DATE_SUB(CURRENT_DATE('Asia/Manila'), INTERVAL 1 DAY)
  AND DATE(o.base_loaded_at) >= DATE_SUB(CURRENT_DATE('Asia/Manila'), INTERVAL 1 DAY)
  AND DATE(a1.base_loaded_at) >= DATE_SUB(CURRENT_DATE('Asia/Manila'), INTERVAL 1 DAY)`)}
