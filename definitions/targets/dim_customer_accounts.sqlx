config {
  type: "incremental",
  tags:["Shopee","active"],
  uniqueKey: ["firebase_id","account_id","product"],
  bigquery: {
    partitionBy: "registration_date",
    updatePartitionFilter:
        "registration_date >= date_sub(CURRENT_DATE('Asia/Manila'), interval 6 MONTH)"
  }
}
/*NOTES: 
1. gets customer_guid from customer_master
2. gogo firebase_id without account_id not included here, will be added once firebase is present in base_f3 core parties
3. xpay accounts with no account_id will not be included, will be added once account_id is available in prod_xpay.customers*/

SELECT 
  m.customer_guid as customer_guid,
  a.*
FROM (
  SELECT COALESCE(CAST(account_id AS STRING), '') as account_id, firebase_id, customer_type,registration_date, registration_time, product from ${ref("dim_gogo_accounts")} ${when(incremental(), `WHERE registration_date >= DATE_SUB(CURRENT_DATE('Asia/Manila'), interval 1 DAY)`)}
  UNION DISTINCT
  SELECT COALESCE(CAST(account_id AS STRING), '') as account_id, firebase_id, customer_type,registration_date, registration_time, product from ${ref("dim_xpay_accounts")} ${when(incremental(), `WHERE registration_date >= DATE_SUB(CURRENT_DATE('Asia/Manila'), interval 1 DAY)`)}
  UNION DISTINCT
  SELECT COALESCE(CAST(account_id AS STRING), '') as account_id, firebase_id, customer_type,registration_date, registration_time, product from ${ref("dim_xpost_accounts")} ${when(incremental(), `WHERE registration_date >= DATE_SUB(CURRENT_DATE('Asia/Manila'), interval 1 DAY)`)}
  UNION DISTINCT
  SELECT COALESCE(CAST(account_id AS STRING), '') as account_id, firebase_id, customer_type,registration_date, registration_time, product from ${ref("dim_paylink_accounts")} ${when(incremental(), `WHERE registration_date >= DATE_SUB(CURRENT_DATE('Asia/Manila'), interval 1 DAY)`)}
  ) a
-- from ${ref("dim_customer_accounts_view")} a
JOIN ${ref("dim_customer_master")} m ON a.firebase_id = m.firebase_id
WHERE a.product IN ('GOGO', 'XPay', 'XPost', 'Paylink', 'GGX Biz')
  AND a.firebase_id IS NOT NULL AND COALESCE(CAST(a.account_id AS STRING),'') <> '' 
${when(incremental(), `AND a.registration_date >= DATE_SUB(CURRENT_DATE('Asia/Manila'), interval 1 DAY)`)}

UNION DISTINCT 

SELECT 
  m.customer_guid as customer_guid,
  a.*
FROM (
  SELECT CAST(account_id AS STRING) as account_id, COALESCE(firebase_id,'') as firebase_id, customer_type,registration_date, registration_time, product from ${ref("dim_shopee_accounts")} ${when(incremental(), `WHERE registration_date >= DATE_SUB(CURRENT_DATE('Asia/Manila'), interval 1 DAY)`)}
  UNION DISTINCT
  SELECT CAST(account_id AS STRING) as account_id, COALESCE(firebase_id,'') as firebase_id, customer_type,registration_date, registration_time, product from ${ref("dim_xpost_accounts")} ${when(incremental(), `WHERE registration_date >= DATE_SUB(CURRENT_DATE('Asia/Manila'), interval 1 DAY)`)}
  UNION DISTINCT
  SELECT CAST(account_id AS STRING) as account_id, COALESCE(firebase_id,'') as firebase_id, customer_type,registration_date, registration_time, product from ${ref("dim_cmo_accounts")} ${when(incremental(), `WHERE registration_date >= DATE_SUB(CURRENT_DATE('Asia/Manila'), interval 1 DAY)`)}
  UNION DISTINCT
  SELECT CAST(account_id AS STRING) as account_id, COALESCE(firebase_id,'') as firebase_id, customer_type,registration_date, registration_time, product from ${ref("dim_lbconline_accounts")} ${when(incremental(), `WHERE registration_date >= DATE_SUB(CURRENT_DATE('Asia/Manila'), interval 1 DAY)`)}
  UNION DISTINCT
  SELECT CAST(account_id AS STRING) as account_id, COALESCE(firebase_id,'') as firebase_id, customer_type,registration_date, registration_time, product from ${ref("dim_lazada_accounts")} ${when(incremental(), `WHERE registration_date >= DATE_SUB(CURRENT_DATE('Asia/Manila'), interval 1 DAY)`)}
  UNION DISTINCT
  SELECT CAST(account_id AS STRING) as account_id, COALESCE(firebase_id,'') as firebase_id, customer_type,registration_date, registration_time, product from ${ref("dim_shippingcart_accounts")} ${when(incremental(), `WHERE registration_date >= DATE_SUB(CURRENT_DATE('Asia/Manila'), interval 1 DAY)`)}
  ) a
-- from ${ref("dim_customer_accounts_view")} a
JOIN ${ref("dim_customer_master")} m ON CAST(a.account_id AS STRING) = CAST(m.account_id AS STRING)
WHERE a.product IN ('Shopee', 'SHP (Reverse Logistics)', 'Shopee (Tiered Pricing)', 'XPost', 'GGX Biz', 'CMO','LBC Online (Account 1)','LBC Online (Account 2)','Lazada','ShippingCart')
  AND COALESCE(a.firebase_id,'') = '' AND a.account_id IS NOT NULL
${when(incremental(), `AND a.registration_date >= DATE_SUB(CURRENT_DATE('Asia/Manila'), interval 1 DAY)`)}
