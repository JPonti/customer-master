config {
  type: "incremental",
  uniqueKey: ["customer_guid","product","address_id"],
  bigquery: {
    partitionBy: "date",
    updatePartitionFilter:
        "date >= DATE_SUB(CURRENT_DATE('Asia/Manila'), interval 1 DAY)"
  }
}

SELECT 
  DISTINCT
  DATE(a.created_at) as date,
  c.account_id,
  c.customer_guid,
  c.product,
  a.id as address_id,
  CONCAT(u.first_name, ' ',IF(u.middle_name IS NULL, '', u.middle_name), IF(u.middle_name IS NULL, '', ' '), u.last_name) name,
  u.email AS email,
  NULL AS phone_number,
  u.phone_number as mobile_number,
  a.line_1,
  NULL as district,
  a.city,
  a.state as province,
  a.region,
  COALESCE(CASE WHEN u.phone_number LIKE '+63%' THEN 'PH'
             WHEN u.phone_number LIKE '+60%' THEN 'MY'
             ELSE IF(DATE(u.created_at,'Asia/Manila') < '2019-03-26' /*PH Only Era*/, 'PH',NULL) END, a.country_code) AS country,
FROM ${ref("dim_customer_accounts")} c
  JOIN ${ref({schema: "base_shippingcart", name: "core_users"})} u on u.party_id = SAFE_CAST(c.account_id AS INT)
  LEFT JOIN ${ref({schema: "base_shippingcart", name: "core_addresses"})} a on a.id = u.address_id
WHERE c.product IN ('ShippingCart')
${when(incremental(), `AND DATE(a.base_loaded_at) >= DATE_SUB(CURRENT_DATE('Asia/Manila'), INTERVAL 1 DAY)`)}