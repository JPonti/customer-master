config {
  type: "incremental",
  uniqueKey: ["account_id","product"],
  bigquery: {
    partitionBy: "registration_date"
  }
}

SELECT
  return_address_id as account_id,
  CAST(NULL AS STRING) as firebase_id,
  'merchant' as customer_type,
  MIN(DATE(o.created_at, 'Asia/Manila')) as registration_date,  
  MIN(DATETIME(o.created_at, 'Asia/Manila')) as registration_time,  
  org.name as product
FROM ${resolve({schema: "base_f3", name: "consumer_orders"})} o
JOIN ${resolve({schema: "base_f3", name: "core_organizations"})} org ON org.party_id = o.service_id
JOIN ${resolve({schema: "base_f3", name: "core_addresses"})} a ON o.return_address_id = a.id
WHERE service_id IN (215744) AND a.name NOT LIKE 'Lazada%' AND a.name NOT IN ('Cabuyao WH','CB WH Reverse')
${when(incremental(), `AND  DATE(o.base_loaded_at) >= DATE_SUB(CURRENT_DATE('Asia/Manila'), interval 1 DAY)
  AND o.return_address_id NOT IN (SELECT account_id FROM ${self()})`)}
GROUP BY 1,2,3,6

