config {
  type: "incremental",
  tags:["ShippingCart","active"],
  uniqueKey: ["account_id","product"],
  bigquery: {
    partitionBy: "registration_date"
  }
}

SELECT 
  p.id AS account_id,
  CAST(NULL AS STRING) as firebase_id,
  'buyer' as customer_type,
  DATE(p.created_at,'Asia/Manila') AS registration_date,
  DATETIME(p.created_at,'Asia/Manila') AS registration_time,
  'ShippingCart' as product
FROM ${ref({schema: "base_shippingcart", name: "core_parties"})} p
  JOIN ${ref({schema: "base_shippingcart", name: "core_users"})} u on u.party_id = p.id
  JOIN ${ref({schema: "base_shippingcart", name: "core_party_roles"})} r on r.party_id = p.id and r.role_id = 1
  LEFT JOIN ${ref({schema: "base_shippingcart", name: "core_addresses"})} a on a.id = u.address_id
  LEFT JOIN ${ref("t_test_accounts")} t ON t.email = u.email AND product = 'ShippingCart'
WHERE p.type = 'user'
  AND t.email IS NULL
  AND p.id NOT IN (
  130604, --Not In System Account
  29761, --Recovery Account
  30290, -- Disposal Account
  97602, --Pull-out Account
  136036, --Murph Memije test account
  10600, --etan test account
  173796, --sc uat acct
  1743) --Test Koko Domingo
  ${when(incremental(), `AND DATE(p.base_loaded_at) >= `)} ${when(incremental(), dataform.projectConfig.vars.start_date)}

